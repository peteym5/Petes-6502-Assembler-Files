PM_MEMORY_AREA     = $7000 
MISSILE_MEMORY     = PM_MEMORY_AREA+$0300
PLAYER_MEMORY_0	   = PM_MEMORY_AREA+$0400
PLAYER_MEMORY_1	   = PM_MEMORY_AREA+$0500
PLAYER_MEMORY_2	   = PM_MEMORY_AREA+$0600
PLAYER_MEMORY_3	   = PM_MEMORY_AREA+$0700


SPRITELENGTH      = 16

SPRITENUM	        = PM_MEMORY_AREA+$0000
SPRITESRC	        = PM_MEMORY_AREA+$0008
SETSP0COLOR	      = PM_MEMORY_AREA+$0010
SETSP1COLOR	      = PM_MEMORY_AREA+$0018
SETSPWIDTH	      = PM_MEMORY_AREA+$0020
SPRITENHOZ	      = PM_MEMORY_AREA+$0028
SPRITENVRT	      = PM_MEMORY_AREA+$0030
SPZONT		        = PM_MEMORY_AREA+$0038
SPZONB            = PM_MEMORY_AREA+$0040

SPRITEUSE         = PM_MEMORY_AREA+$0048

SPSRC0           	= PM_MEMORY_AREA+$0060
SPSRC1           	= PM_MEMORY_AREA+$0078
SPSRC2           	= PM_MEMORY_AREA+$0090
SPSRC3           	= PM_MEMORY_AREA+$00A8

ZPTEMP0            = $D0
ZPTEMP1            = $D1




Sprite_Multi_Plex	= * ;Mono
    LDY #15
    STY CURRENTSPRITE         
SET_PLEX_LOOP	    	
    LDY CURRENTSPRITE     
    LDA SPHOZNEXT,Y
    STA SPRITENHOZ,Y
    LDX SPRITENVRT,Y       
    LDA :SPRITE_TABZONE+ 0 ,X
    STA SPZONT,Y
    BMI IsZoneNeg
    TXA
    CLC
    ADC # (SPRITELENGTH+1) 
    TAX
IsZoneNeg    
    LDA :SPRITE_TABZONE+ 0 ,X
    STA SPZONB,Y
    BMI PrepareNextNdx
    TXA
    LDX :PLAYER_ADDR_LIST,Y
    STA STORPLYR0ADDRLO,X
    LDA SPRITENUM,Y    				
    TAY			
    LDA :SPRARDLO,Y
    STA LOADPLYRADDRLO,X
    LDA :SPRARDHI,Y
    STA LOADPLYRADDRHI,X
PrepareNextNdx      
    DEC	CURRENTSPRITE	 
    BPL SET_PLEX_LOOP
    
    LDA FRAMECLOCKLO
    AND #15
    STA VLASTSPRITENO
    STA CURRENTSPRITE
    
    TAY
    LDA SPRITENVRT,Y
    BEQ SKIPSPRITE
DRAWSPRITELOOP
    LDX SPZONB,Y      ;4
    BMI SKIPSPRITE ;2
    STX ZPTEMP0            ;3
    LDA SPRITEUSE,X   ;4
    LDX SPZONT,Y      ;4
    ORA SPRITEUSE,X   ;4 ;24	
    STA ZPTEMP1
    LDA #1
    BIT ZPTEMP1 
    BNE CKUSP1
PML0
    TYA
    STA SPSRC0,X
    LDA SPRITEUSE,X    
    ORA #1
    STA SPRITEUSE,X
    CPX ZPTEMP0
    INX
    BCC PML0
    LDA # >PLAYER_MEMORY_0
    JMP DOQIKSPR
CKUSP1
    LDA #2
    BIT ZPTEMP1 
    BNE CKUSP2
PML1    	
    TYA
    STA SPSRC1,X    
    LDA SPRITEUSE,X    
    ORA #2
    STA SPRITEUSE,X
    CPX ZPTEMP0
    INX
    BCC PML1
    LDA # >PLAYER_MEMORY_1
    JMP DOQIKSPR
CKUSP2
    LDA #4
    BIT ZPTEMP1 
    BNE CKUSP3
PML2    
    TYA
    STA SPSRC2,X    
    LDA SPRITEUSE,X
    ORA #4
    STA SPRITEUSE,X
    CPX ZPTEMP0
    INX
    BCC PML2
    LDA # >PLAYER_MEMORY_2
    JMP DOQIKSPR
CKUSP3
    LDA #8
    BIT ZPTEMP1 
    BNE SKIPSPRITE
DOPM3    
PML3    
    TYA
    STA SPSRC3,X    
    LDA SPRITEUSE,X
    ORA #8
    STA SPRITEUSE,X
    CPX ZPTEMP0
    INX
    BCC PML3
    LDA # >PLAYER_MEMORY_3
    JMP DOQIKSPR
SKIPSPRITE
    LDX PLAYER_ADDR_LIST,Y
    LDA #0
    STA STORPLYR0ADDRLO,X 
    JMP NEXTSPRITE

DOQIKSPR
    LDX PLAYER_ADDR_LIST,Y
    STA STORPLYR0ADDRHI,X
NEXTSPRITE    
    LDA NEXT_SPRITE_LIST,Y
    TAY
    STY CURRENTSPRITE

CHECKLAST

VLASTSPRITENO = *+1
    CPY #03
    BEQ DOPMXFER
    JMP DRAWSPRITELOOP 
DOPMXFER
    LDY #SPRITELENGTH-1
DRAWPLAYERLOOP
LOADPLYRADDRLO = *+1
LOADPLYRADDRHI = *+2
STORPLYR0ADDRLO = *+4
STORPLYR0ADDRHI = *+5
    LDA $FFFF,Y    ;0
    STA $FE00,Y    
    LDA $FFFF,Y    ;1
    STA $FE00,Y
    LDA $FFFF,Y    ;2
    STA $FE00,Y
    LDA $FFFF,Y    ;3
    STA $FE00,Y
    LDA $FFFF,Y    ;4
    STA $FE00,Y
    LDA $FFFF,Y    ;5
    STA $FE00,Y
    LDA $FFFF,Y    ;6
    STA $FE00,Y
    LDA $FFFF,Y    ;7
    STA $FE00,Y
    LDA $FFFF,Y    ;8
    STA $FE00,Y    
    LDA $FFFF,Y    ;9
    STA $FE00,Y
    LDA $FFFF,Y    ;10
    STA $FE00,Y
    LDA $FFFF,Y    ;11
    STA $FE00,Y
    LDA $FFFF,Y    ;12
    STA $FE00,Y
    LDA $FFFF,Y    ;13
    STA $FE00,Y
    LDA $FFFF,Y    ;14
    STA $FE00,Y
    LDA $FFFF,Y    ;15
    STA $FE00,Y
    DEY 
    BPL DRAWPLAYERLOOP
   
PLAYERDRAWDONE

		LDY #15
PostAddrDeleteLoop
		LDX PLAYER_ADDR_LIST,Y
		LDA STORPLYR0ADDRHI,X
		LDX DELETE_ADDR_LIST,Y
		STA ADDRPAGE0DEL,x
		LDA SPRITENVRT,Y
		STA ADDRPLAY0DEL,x
  	LDA #$00
    STA STORPLYR0ADDRLO,X
    STA SPRITEUSE,Y
		DEY
		BPL PostAddrDeleteLoop  

  	INC FRAMECLOCKLO
  	BNE NOCLHI
  	INC FRAMECLOCKHI  
NOCLHI

VBIADR = *+1
    JMP ARPG_VBI

Clear_Plex_Sprites
	LDA #0
	STA ZONDX
NXTDEL
	LDY #SPRITELENGTH - 1
DLOOP0
ADDRPLAY0DEL = *+1
ADDRPAGE0DEL = *+2
	STA PLAYER_MEMORY_1,Y ;0
	STA PLAYER_MEMORY_1,Y ;1
	STA PLAYER_MEMORY_1,Y ;2
	STA PLAYER_MEMORY_1,Y ;3
	STA PLAYER_MEMORY_1,Y ;4
	STA PLAYER_MEMORY_1,Y ;5
	STA PLAYER_MEMORY_1,Y ;6
	STA PLAYER_MEMORY_1,Y ;7
	STA PLAYER_MEMORY_1,Y ;8
	STA PLAYER_MEMORY_1,Y ;9
	STA PLAYER_MEMORY_1,Y ;10
	STA PLAYER_MEMORY_1,Y ;11
	STA PLAYER_MEMORY_1,Y ;12
	STA PLAYER_MEMORY_1,Y ;13
	STA PLAYER_MEMORY_1,Y ;14
	STA PLAYER_MEMORY_1,Y ;15
	DEY
	BPL DLOOP0

	STA HPOSP0
	STA HPOSP1
	STA HPOSP2
	STA HPOSP3

	LDX DLIXSAV
	LDY DLIYSAV
	PLA
	RTI

Clear_Sprite_Memory
	lda #$00		
	STA HPOSP0		
	STA HPOSP1		
	STA HPOSP2		
	STA HPOSP3		
	STA HPOSM0		
	STA HPOSM1		
	STA HPOSM2		
	STA HPOSM3		
	ldy #$FF	
Clear_Sprite_Loop		
	sta MISSILE_MEMORY,Y	
	sta PLAYER_MEMORY_0,Y	
	sta PLAYER_MEMORY_1,Y	
	sta PLAYER_MEMORY_2,Y	
	sta PLAYER_MEMORY_3,Y	
	dey		
	bne Clear_Sprite_Loop
	sta COLPM0		
	sta COLPM1		
	sta COLPM2		
	sta COLPM3		

	
	ldy #$15
Clear_Sprite_Data_Loop
	sta SETSPWIDTH,Y
	sta SPRITENUM,Y	
	sta SETSP0COLOR,Y
	sta SETSPWIDTH,Y
	sta SPRITENHOZ,Y
	sta SPRITENVRT,Y
	ldx PLAYER_ADDR_LIST,Y
	sta STORPLYR0ADDRLO,x
	dey		
	bpl Clear_Sprite_Data_Loop
	rts			


SB_DLI
    PHA
    LDA #<PT_DLI
    STA VDSLST + 0
    LDA #>PT_DLI
    STA VDSLST + 1
    LDA #10
    STA COLPF1
    LDA #00
    STA COLPF2
    LDA #$D8
    STA COLPF0   
    LDA #$56
    STA COLPF3
    PLA
    RTI

PT_DLI
    PHA
    TXA
    PHA
    TYA 
    PHA

    LDA #$14
    STA COLPF0
    STX WSYNC    
    LDA #<PF_DLI
    LDX #>PF_DLI
    STA VDSLST + 0        
    STX VDSLST + 1
    LDA #$34
    LDX #$22
    STA COLPF1
    STX COLPF2               
    PLA
    TAY
    PLA
    TAX
		PLA
    RTI

PF_DLI
    PHA
    TXA
    PHA
    TYA 
    PHA
  
    LDY ZONDX
    LDX SPSRC0,Y
    LDA SPRITENHOZ,X
    STA HPOSP0
    LDA SPRITE0COLOR,X
    STA COLPM0
           
    LDX SPSRC1,Y
    LDA SPRITENHOZ,X
    STA HPOSP1
    LDA SPRITE0COLOR,X
    STA COLPM1
            
    LDX SPSRC2,Y
    LDA SPRITENHOZ,X
    STA HPOSP2
    LDA SPRITE0COLOR,X
    STA COLPM2
            
    LDX SPSRC3,Y
    LDA SPRITENHOZ,X
    STA HPOSP3
    LDA SPRITE0COLOR,X
    STA COLPM3
     
    INY
    STY ZONDX
  
    CPY #23
    BEQ Last_PF_Line

    PLA
    TAY
    PLA
    TAX
		PLA
		RTI
Last_PF_Line
    JMP Clear_Plex_Sprites

NEXT_SPRITE_LIST
    DTA 5,6,7,8,9,10,11,12,13,14,15,0,1,2,3,4
    
DSVANDMASK
    DTA 254,253,251,247,239,223,191,127


SPRITE_TABZONE

    DTA 255,255,255,255, 255,255,255,255
    DTA 255,255,255,255, 255,255,255,255
    DTA 255,255,255,255, 255,255,255,255
    DTA 255,255,255,255, 000,000,000,000,000, 000,000
    
    DTA 000,000,000,000,000,000,000,000,000
    DTA 00,00,00,00,00,00,00,00
    DTA 00,00,00,00,00,00,00,00
    DTA 01,01,01,01,01,01,01,01             
    DTA 02,02,02,02,02,02,02,02
    DTA 03,03,03,03,03,03,03,03  
    DTA 04,04,04,04,04,04,04,04
    DTA 05,05,05,05,05,05,05,05 
    DTA 06,06,06,06,06,06,06,06 
    DTA 07,07,07,07,07,07,07,07
    DTA 08,08,08,08,08,08,08,08
    DTA 09,09,09,09,09,09,09,09
    DTA 10,10,10,10,10,10,10,10
    DTA 11,11,11,11,11,11,11,11
    DTA 12,12,12,12,12,12,12,12
    DTA 13,13,13,13,13,13,13,13
    DTA 14,14,14,14,14,14,14,14     
    DTA 15,15,15,15,15,15,15,15
    DTA 16,16,16,16,16,16,16,16
    DTA 17,17,17,17,17,17,17,17
    DTA 18,18,18,18,18,18,18,18
    DTA 19,19,19,19,19,19,19,19
    DTA 20,20,20,20,20,20,20,20
    DTA 21,21,21,21,21,21,21,21
    DTA 22,22,22,22,22,22,22,22
    DTA 22,22,22,22,22,22,22,22
    DTA 22,22,255,255,255,255,255
    DTA 255,255,255,255,255,255


